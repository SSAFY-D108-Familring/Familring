plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
}

// 루트를 포함한 모든 프로젝트에 영항을 주는 세팅
allprojects {
    group = 'com.familring'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

// 하위 모듈들에 영향을 주는 세팅
subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    // 하위 모듈들이 사용하는 공통 dependencies
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // StringUtils
        implementation 'org.apache.commons:commons-lang3:3.16.0'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

bootJar {
    enabled = false
}
